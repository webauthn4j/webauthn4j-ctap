/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath libs.kotlin.gradle.plugin
        classpath libs.android.tools.build.gradle
        classpath libs.oss.licenses.plugin
        classpath libs.firebase.crashlytics.gradle
        classpath libs.google.services
        classpath libs.androidx.navigation.safe.args.gradle.plugin

        classpath libs.asciidoctor.gradle.jvm
        classpath libs.sonarqube.gradle.plugin

        // workaround from https://issuetracker.google.com/issues/159151549
        classpath libs.asm
        classpath libs.asm.util
        classpath libs.asm.commons

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.google.devtools.ksp' version '1.9.21-1.0.16' apply false
}

allprojects {
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

repositories {
    google()
    mavenCentral()
}

// To use dependencies block in this parent file, apply plugins in this file first
project(':webauthn4j-ctap-core') {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
}
project(':webauthn4j-ctap-authenticator') {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
}
project(':webauthn4j-ctap-client') {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
}
project(':unifidokey-core') {
    apply plugin: 'com.android.library'
}
project(':unifidokey-handheld') {
    apply plugin: 'com.android.application'
}

subprojects {

    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.jfrog.org/artifactory/libs-snapshot" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        //BOM
        implementation platform("com.google.firebase:firebase-bom:32.7.0")
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

import org.asciidoctor.gradle.jvm.AsciidoctorTask

task generateUserGuide(type: AsciidoctorTask) {
    group = "documentation"
    baseDirFollowsSourceDir()
    sourceDir = file("docs/user-guide")
    outputDir = file("build/docs/asciidoc/html5")
    options eruby: 'erubis'
    //noinspection GroovyAssignabilityCheck
    attributes docinfo: '',
            copycss: '',
            icons: 'font',
            'source-highlighter': 'prettify',
            sectanchors: '',
            toc2: '',
            idprefix: '',
            idseparator: '-',
            doctype: 'book',
            numbered: '',
            revnumber: "${unifidoKeyVersion}"
}

task generateReleaseNote(type: JavaExec) {
    group = "documentation"
    classpath = files('gradle/lib/github-release-notes-generator.jar')

    args(unifidoKeyVersion, file("build/release-note.md").absolutePath, "--spring.config.location=file:" + file("github-release-notes-generator.yml").absolutePath)

}

sonarqube {
    properties {
        property 'sonar.projectKey', 'webauthn4j-ctap'
        property 'sonar.issue.ignore.multicriteria', 'e1,e2,e3'
        property 'sonar.issue.ignore.multicriteria.e1.ruleKey', 'java:S110'
        property 'sonar.issue.ignore.multicriteria.e1.resourceKey', '**/*.java'
        property 'sonar.issue.ignore.multicriteria.e2.ruleKey', 'java:S1452'
        property 'sonar.issue.ignore.multicriteria.e2.resourceKey', '**/*.java'
        property 'sonar.issue.ignore.multicriteria.e3.ruleKey', 'common-java:DuplicatedBlocks'
        property 'sonar.issue.ignore.multicriteria.e3.resourceKey', '**/*.java'
    }
}

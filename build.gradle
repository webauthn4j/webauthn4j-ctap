/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    //UnifidoKey
    ext.unifidoKeyVersion = '0.7.0-SNAPSHOT'

    //kotlin
    ext.kotlinVersion = '1.5.30'
    ext.coreKtxVersion = "1.6.0"
    ext.kotlinxCoroutinesVersion = "1.5.1-native-mt"

    ext.daggerVersion = "2.38.1"
    ext.roomVersion = "2.3.0"

    // Android libraries
    ext.asmVersion = "9.2"
    ext.appcompatVersion = "1.3.1"
    ext.constraintlayoutVersion = "2.1.0"
    ext.biometricVersion = "1.1.0"
    ext.legacySupportV4Version = "1.0.0"
    ext.preferenceVersion = "1.1.1"
    ext.lifecycleExtensions = "2.2.0"
    ext.navigationVersion = "2.3.5"
    ext.lifecycleKtxVersion = "2.3.1"
    ext.materialVersion = "1.4.0"
    ext.googleServicesVersion = "4.3.10"
    ext.firebaseBomVersion = "28.4.0"
    ext.firebaseCrashlyticsGradleVersion = "2.7.1"
    ext.googlePlayServicesAuthVersion = "19.2.0"
    ext.googlePlayServicesFidoVersion = "18.1.0"
    ext.googlePlayServicesSafetyNetVersion = "17.0.1"
    ext.googlePlayServicesOSSLicensesVersion = "17.0.0"
    ext.androidGradlePluginVersion = "7.0.2"
    ext.navigationSafeArgsGradlePluginVersion = "2.4.0-alpha08"
    ext.ossLicensesPluginVersion = "0.10.4"

    // Third party libraries
    ext.springBootVersion = "2.5.4"
    ext.bouncyCastleVersion = "1.69"
    ext.logbackAndroidVersion = "2.0.0"
    ext.webauthn4jVersion = "master-SNAPSHOT"
    ext.mockitoVersion = "3.12.4"
    ext.mockitoKotlinVersion = "3.2.0"
    ext.asciidoctorGradleVersion = "3.3.2"

    // Test libraries
    ext.truthVersion = "1.1.3"
    ext.androidxTestVersion = "1.4.0"
    ext.androidxTestExtJUnitVersion = "1.1.3"
    ext.espressoVersion = "3.4.0"
    ext.androidSupportVersion = "28.0.0"
    ext.robolectricVersion = "4.6.1"
    ext.sonarqubeVersion = "3.3"

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "com.google.android.gms:oss-licenses-plugin:$ossLicensesPluginVersion"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$firebaseCrashlyticsGradleVersion"
        classpath "com.google.gms:google-services:$googleServicesVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationSafeArgsGradlePluginVersion"

        classpath "org.asciidoctor:asciidoctor-gradle-jvm:$asciidoctorGradleVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubeVersion"

        // workaround from https://issuetracker.google.com/issues/159151549
        classpath "org.ow2.asm:asm:$asmVersion"
        classpath "org.ow2.asm:asm-util:$asmVersion"
        classpath "org.ow2.asm:asm-commons:$asmVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'org.sonarqube'

allprojects {
    // Load secret.properties if it exists
    if (new File("$project.rootDir/secret.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream("$project.rootDir/secret.properties"))
        props.each { prop ->
            project.ext.set(prop.key, prop.value)
        }
    }

    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

repositories {
    google()
    mavenCentral()
}

// To use dependencies block in this parent file, apply plugins in this file first
project(':webauthn4j-ctap') {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'
}
project(':unifidokey-core') {
    apply plugin: 'com.android.library'
}
project(':unifidokey-handheld') {
    apply plugin: 'com.android.application'
}

subprojects {

    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.jfrog.org/artifactory/libs-snapshot" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        constraints {
            // kotlin

            implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
            implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8")
            implementation("androidx.core:core-ktx:$coreKtxVersion")
            implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleKtxVersion")
            implementation("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleKtxVersion")
            implementation("androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleKtxVersion")

            // google
            implementation("androidx.navigation:navigation-fragment:$navigationVersion")
            implementation("androidx.navigation:navigation-ui:$navigationVersion")

            implementation("androidx.room:room-runtime:$roomVersion")
            implementation("androidx.appcompat:appcompat:$appcompatVersion")
            implementation("androidx.constraintlayout:constraintlayout:$constraintlayoutVersion")

            implementation("androidx.biometric:biometric:$biometricVersion")
            implementation("androidx.legacy:legacy-support-v4:$legacySupportV4Version")
            implementation("androidx.preference:preference:$preferenceVersion")
            implementation("androidx.lifecycle:lifecycle-livedata:$lifecycleExtensions")
            implementation("androidx.lifecycle:lifecycle-extensions:$lifecycleExtensions")

            implementation("com.google.dagger:dagger:$daggerVersion")
            implementation("com.google.dagger:dagger-android:$daggerVersion")
            implementation("com.google.dagger:dagger-android-support:$daggerVersion")

            implementation("com.google.android.material:material:$materialVersion")

            implementation("com.google.android.gms:play-services-auth:$googlePlayServicesAuthVersion")
            implementation("com.google.android.gms:play-services-fido:$googlePlayServicesFidoVersion")
            implementation("com.google.android.gms:play-services-safetynet:$googlePlayServicesSafetyNetVersion")
            implementation("com.google.android.gms:play-services-oss-licenses:$googlePlayServicesOSSLicensesVersion")

            // third party
            implementation("org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion")
            implementation("org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion")
            implementation("com.github.webauthn4j.webauthn4j:webauthn4j-util:$webauthn4jVersion")
            implementation("com.github.webauthn4j.webauthn4j:webauthn4j-core:$webauthn4jVersion")
            implementation("com.github.webauthn4j.webauthn4j:webauthn4j-test:$webauthn4jVersion")
            implementation("org.mockito:mockito-core:$mockitoVersion")
            implementation("org.mockito:mockito-inline:$mockitoVersion")
            implementation("org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion")
            implementation("com.github.tony19:logback-android:$logbackAndroidVersion")

            // test
            implementation("org.jetbrains.kotlinx:kotlinx-coroutines-test")
            implementation("com.google.truth:truth:$truthVersion")
            implementation("com.google.truth.extensions:truth-java8-extension:$truthVersion")
            implementation("androidx.test:core:$androidxTestVersion")
            implementation("androidx.test.ext:junit:$androidxTestExtJUnitVersion")
            implementation("androidx.test:runner:$androidxTestVersion")
            implementation("androidx.test.espresso:espresso-core:$espressoVersion")
            implementation("com.android.support:support-annotations:$androidSupportVersion")
            implementation("org.robolectric:robolectric:$robolectricVersion")
        }

        //BOM
        implementation platform("com.google.firebase:firebase-bom:${firebaseBomVersion}")
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

import org.asciidoctor.gradle.jvm.AsciidoctorTask

task generateUserGuideJA(type: AsciidoctorTask) {
    group = "documentation"
    baseDirFollowsSourceDir()
    sourceDir = file("docs/user-guide/ja")
    outputDir = file("build/docs/asciidoc/html5/ja")
    options eruby: 'erubis'
    //noinspection GroovyAssignabilityCheck
    attributes docinfo: '',
            copycss: '',
            icons: 'font',
            'source-highlighter': 'prettify',
            sectanchors: '',
            toc2: '',
            idprefix: '',
            idseparator: '-',
            doctype: 'book',
            numbered: '',
            revnumber: "${unifidoKeyVersion}"
}

task generateUserGuideEN(type: AsciidoctorTask) {
    group = "documentation"
    baseDirFollowsSourceDir()
    sourceDir = file("docs/user-guide/en")
    outputDir = file("build/docs/asciidoc/html5/en")
    options eruby: 'erubis'
    //noinspection GroovyAssignabilityCheck
    attributes docinfo: '',
            copycss: '',
            icons: 'font',
            'source-highlighter': 'prettify',
            sectanchors: '',
            toc2: '',
            idprefix: '',
            idseparator: '-',
            doctype: 'book',
            numbered: '',
            revnumber: "${unifidoKeyVersion}"
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'webauthn4j-ctap'
        property 'sonar.issue.ignore.multicriteria', 'e1,e2,e3'
        property 'sonar.issue.ignore.multicriteria.e1.ruleKey', 'java:S110'
        property 'sonar.issue.ignore.multicriteria.e1.resourceKey', '**/*.java'
        property 'sonar.issue.ignore.multicriteria.e2.ruleKey', 'java:S1452'
        property 'sonar.issue.ignore.multicriteria.e2.resourceKey', '**/*.java'
        property 'sonar.issue.ignore.multicriteria.e3.ruleKey', 'common-java:DuplicatedBlocks'
        property 'sonar.issue.ignore.multicriteria.e3.resourceKey', '**/*.java'
    }
}
